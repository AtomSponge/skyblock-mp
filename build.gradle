buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

project.ext {
    mcversion = "1.7.10"
    major = "1"
    minor = "0"
    revision = "0"
}

version = "${project.mcversion}-${project.major}.${project.minor}.${project.revision}"
group = "com.github.atomsponge.skyblockmp"
archivesBaseName = "skyblockmp"

sourceCompatibility = 1.7
targetCompatibility = sourceCompatibility

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "run"

    srgExtra "PK: org/slf4j com/github/atomsponge/skyblockmp/libs/org/slf4j"

    replaceIn "Constants.java"
    replace "@VERSION@", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    shade group: 'com.zaxxer', name: 'HikariCP', version: '2.3.13'
    // I'd use slf4j-log4j12, but apparently Forge seems to be using an old version of log4j...
    shade group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.18'
    shade group: 'com.h2database', name: 'h2', version: '1.4.191'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    // See https://forgegradle.readthedocs.org/en/FG_1.2/user-guide/shading/
    configurations.shade.each {
        dep -> from (project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
